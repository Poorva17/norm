plugins {
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.12.0"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":cli")
    compileOnly gradleApi()
    testImplementation gradleTestKit()
    testImplementation "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72"
}

project.afterEvaluate {
    tasks.withType(ProcessResources) { processResources ->
        inputs.property('projectVersion', project.version)
        duplicatesStrategy = DuplicatesStrategy.WARN

        filesMatching("version.properties") {
            expand('projectVersion': project.version)
        }
    }
}

gradlePlugin {
    plugins {
        register("norm") {
            id = "com.medly.norm"
            implementationClass = "com.medly.norm.NormPlugin"
        }
    }
}

pluginBundle {
    website = 'https://github.com/medly/norm'
    vcsUrl = 'https://github.com/medly/norm'
    description = "Generate kotlin data classes from SQL queries using NORM"
    tags = ['kotlin', 'postgres', 'codegen']

    mavenCoordinates {
        groupId = project.group.toString()
        artifactId = project.name
        version = project.version.toString()
    }

    plugins {
        norm {
            id = "com.medly.norm"
            displayName = "Norm Gradle Plugin"
        }
    }
}

